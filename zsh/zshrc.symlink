# ------------------------------------------------------------------------------
# Don't load anything for Cursor terminal
# https://forum.cursor.com/t/guide-fix-cursor-agent-terminal-hangs-caused-by-zshrc/107260
# ------------------------------------------------------------------------------
# if [[ "$PAGER" == "head -n 10000 | cat" || "$COMPOSER_NO_INTERACTION" == "1" ]]; then
#   echo "Cursor terminal detected, skipping zshrc"
#   return
# fi

# ------------------------------------------------------------------------------
# Powerlevel10k Instant Prompt (Keep this at the very top)
# ------------------------------------------------------------------------------
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ------------------------------------------------------------------------------
# Essential User Configuration & Environment
# ------------------------------------------------------------------------------
export DOTZSH=$HOME/.dotfiles
# project folder that we can `c [tab]` to
export PROJECTS=$HOME/dev

# Source local, non-versioned configurations
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local
[[ -f ~/.secrets.local ]] && source ~/.secrets.local

# ------------------------------------------------------------------------------
# Oh My Zsh (OMZ) and Powerlevel10k (P10k) Theme Setup
# ------------------------------------------------------------------------------
export ZSH=$HOME/.oh-my-zsh

# oh-my-zsh configuration file (defines ZSH_THEME, plugins, etc.)
# This file specifies ZSH_THEME ("powerlevel10k/powerlevel10k") and zsh plugins
source "$HOME/.ohmyzsh.config"

# Powerlevel10k theme configuration
source ~/.p10k.zsh

# Source oh-my-zsh
source "$ZSH/oh-my-zsh.sh"

# ------------------------------------------------------------------------------
# Custom Dotfiles Topic Files (Paths, Aliases, Functions)
# ------------------------------------------------------------------------------
typeset -U config_files
config_files=($DOTZSH/**/*.zsh)

# Load path files first
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# Load everything else but path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Load custom completions (after OMZ and compinit have run)
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# ------------------------------------------------------------------------------
# Zsh Enhancements (Load towards the end)
# ------------------------------------------------------------------------------

# zsh-syntax-highlighting (Load this last or very late)
source "$(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# Auto-rebuild zcompdump when completion functions change
# This will automatically rebuild the completion cache if it's older than 24 hours
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# To re-source this config, see functions.zsh or run reload!
